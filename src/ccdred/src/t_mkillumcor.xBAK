include	"ccdred.h"

# T_MKILLUMCOR -- Make flat field illumination correction images.
#
# The input flat field images are processed and smoothed to obtain
# illumination correction images.  These illumination correction images
# are used to correct already processed images for illumination effects
# introduced by the flat field.

procedure t_mkillumcor()

int	listin			# List of input CCD images
int	listout			# List of output CCD images
int	ccdtype			# CCD image type
int	interactive		# Fit overscan interactively?

int	proc, calproc, listproc
bool	clgetb(), streq()
int	imtopenp(), imtgetim()
pointer	sp, input, output, tmp, str, in, out, bpm, ccd, immap()
errchk	immap, set_input, set_output, ccddelete

begin
	call smark (sp)
	call salloc (input, SZ_FNAME, TY_CHAR)
	call salloc (output, SZ_FNAME, TY_CHAR)
	call salloc (tmp, SZ_FNAME, TY_CHAR)
	call salloc (str, SZ_LINE, TY_CHAR)

	# Get the lists and instrument translation file.  Open the translation
	# file.  Initialize the interactive flag and the calibration images.

	listin = imtopenp ("input")
	listout = imtopenp ("output")
	call clgstr ("instrument", Memc[input], SZ_FNAME)
	call hdmopen (Memc[input])
	call set_interactive ("", interactive)
	call cal_open (NULL)
	call ccd_open (0, 65536)
	call ccdlog_open (10 * SZ_LINE)
	if (clgetb ("noproc")) {
	    proc = NO
	    calproc = NO
	    listproc = YES
	} else {
	    proc = YES
	    calproc = NO
	    listproc = NO
	}

	# Process each image.
	while (imtgetim (listin, Memc[input], SZ_FNAME) != EOF) {
	    if (listproc == YES) {
		call printf ("%s: mkillumcor\n")
		    call pargstr (Memc[input])
	    }

	    #  Set input and output images.
	    call set_input (Memc[input], in, ccdtype)
	    if (in == NULL)
		next

	    if (imtgetim (listout, Memc[output], SZ_FNAME) == EOF)
		call strcpy (Memc[input], Memc[output], SZ_FNAME)
	    if (Memc[output] == EOS)
		call strcpy (Memc[input], Memc[output], SZ_FNAME)
	    if (streq (Memc[input], Memc[output]))
	        call mktemp ("tmp", Memc[tmp], SZ_FNAME)
	    else
		call strcpy (Memc[output], Memc[tmp], SZ_FNAME)

	    # Process image as an illumination image.
	    call set_proc (in, proc, calproc, listproc, ccd)
	    call set_sections (ccd)
	    call set_trim (ccd)
	    call set_fixpix (ccd)
	    call set_overscan (ccd)
	    call set_zero (ccd)
	    call set_dark (ccd)
	    CORS(ccd, FINDMEAN) = YES
	    call set_output (Memc[tmp], ccd, in, out, bpm)

	    # Do the processing if the COR flag is set.
	    if (PROC(ccd) == YES) {
		if (COR(ccd) == YES) {
		    call ccdlog_flush ()
		    call doproc (ccd)
		    call set_header (ccd)

		    # Replace the input image by the corrected image.
		    call imunmap (in)
		    call imunmap (out)
		    if (streq (Memc[input], Memc[output])) {
			call ccddelete (Memc[input])
			call imrename (Memc[tmp], Memc[input])
		    } else
			call strcpy (Memc[output], Memc[input], SZ_FNAME)
		} else {
		    # Make a copy if necessary.
		    call imunmap (in)
		    call imunmap (out)
		    call imdelete (Memc[tmp])
		    call ccdlog_clear ()
		}

		# Do special processing.
		call mkillumination (Memc[input], Memc[output], YES, YES)
		if (!streq (Memc[input], Memc[output]))
		    call imcopy (Memc[input], Memc[output])
	    } else {
		iferr (call imunmap(out))
		    ;
		iferr (call imdelete (Memc[tmp]))
		    ;
		call imunmap (in)
		call ccdlog_clear ()
	    }
	    call free_proc (ccd)
	    call ccdlog_flush ()
	}

	# Finish up.
	call hdmclose ()
	call imtclose (listin)
	call imtclose (listout)
	call cal_close ()
	call ccdlog_close ()
	call ccd_close ()
	call sfree (sp)
end
